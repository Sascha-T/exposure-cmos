plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    modImplementation "maven.modrinth:exposure:$rootProject.exposure_fabric"
    modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:$rootProject.forgeconfigapiport_fabric"
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    modImplementation "cc.tweaked:cc-tweaked-$rootProject.minecraft_version-fabric:$rootProject.computercraft_clean"
    modImplementation "cc.tweaked:cc-tweaked-$rootProject.minecraft_version-fabric-api:$rootProject.computercraft_clean"
    implementation "cc.tweaked:cc-tweaked-$rootProject.minecraft_version-core:$rootProject.computercraft_clean"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'exposure', rootProject.exposure_clean
    inputs.property 'computercraft', rootProject.computercraft_clean

    filesMatching('fabric.mod.json') {
        expand inputs.properties
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'out'
}

remapJar {
    input.set shadowJar.archiveFile
}
